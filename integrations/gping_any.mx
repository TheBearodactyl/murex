!if { which gping } then {
    return
}

autocomplete set gping %[{
    DynamicDesc: '{
        autocomplete.gping
    }'
}]

private autocomplete.gping {
    # autocompletions for gping: https://github.com/orf/gping

    test: define notempty %{
        StdoutRegex: '--cmd'
    }

    gping --help -> [Options..]re -> foreach line {
        if { $line =~ '^[\s]+-' } then {
            $flag = $line
        } else {
            !if { is-null flag } then {
                out "$flag\t$line"
                !set flag
            }
        }
    } -> :str: mtac <test_notempty> -> tabulate --map --split-comma --key-inc-hint
}

test unit private autocomplete.gping %{
    StdoutGreaterThan: 20
}
